import org.eclipse.jgit.api.Git

group 'com.michaeljperri.flutter_sequencer'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.5.21'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.eclipse.jgit:org.eclipse.jgit:5.12.0+"
    }
}

rootProject.allprojects {
    repositories {
        google()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 24

        externalNativeBuild {
            cmake {
                cppFlags '-std=c++17', '-frelaxed-template-template-args'
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }

    ndkVersion "26.0.10792818"
    externalNativeBuild {
        cmake {
            version "3.22.1"
            path "CMakeLists.txt"
        }
    }
}

void cloneThirdPartyRepo(String name, String uri, String commit) {
    def dirPath = "$projectDir/third_party/$name"
    def dir = new File(dirPath)

    if (!dir.exists()) {
        dir.mkdirs()
        def repo =
            Git.cloneRepository()
                .setDirectory(dir)
                .setURI(uri)
                .setRemote('origin')
                .setCloneSubmodules(true)
                .call()

        repo.checkout().setName(commit).call()
        repo.submoduleUpdate()
        repo.submoduleInit()
    }
}

task cloneThirdPartyRepos {
    //cloneThirdPartyRepo('oboe', 'https://github.com/google/oboe', '8740d0fc321a55489dbbf6067298201b7d2e106d')
    cloneThirdPartyRepo('oboe', 'https://github.com/google/oboe', '0abad3e3412234d9a345755e7ad6b72390110ab1')
    cloneThirdPartyRepo('TinySoundFont', 'https://github.com/schellingb/TinySoundFont.git', 'd21fe7b22c8c906e749e4be4bb389efc2e761964')
    cloneThirdPartyRepo('sfizz', 'https://github.com/sfztools/sfizz.git', '4174e8e00f998f4678547814c0108d74c210e9ad')
}

preBuild.dependsOn cloneThirdPartyRepos

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
